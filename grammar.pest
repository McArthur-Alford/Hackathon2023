// Building Blocks:
alpha = _{ 'a'..'z' | 'A'..'Z' }
digit = _{ '0'..'9' }
false = { "False" }
true = { "True" }
bool = { false | true }
comment = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }
whitespace = _{ comment | " " | "\t" }
char = _{ ASCII_ALPHA | digit | whitespace | ":" | "!" | "." }
string = { char+ }
number = { digit+ }

// Constants and Identifiers:
ident = { (alpha | "_") ~ (alpha | digit | "_")* }
generic = { "<" ~ ident ~ ">" }

// Variable
var = { generic }

// Attribute Handle
attr = { "^" ~ (ident | generic ) }

// Literal types
constant = { string }

// Expressions used in RHS of WME
expr = { var | constant }

// Working Memory Element:
wme = { 
	whitespace*
	~ (var | "*")
	~ (
		whitespace*
		~ (attr | ("^*"))
		~ (
			whitespace*
			~ (expr | "*")
		)?
	)?
}

block = {
	(wme | whitespace+)
	~ (NEWLINE* ~ ((wme ~ whitespace*) | whitespace+))*
}

// Productions:
prod = { 
	ident
	~ ":"
	~ NEWLINE?
	~ block
	~ (
		NEWLINE?
		~ whitespace*
		~ "->"
		~ NEWLINE?
		~ block	
	)?
	~ whitespace*
}

// Entire Program:
file = { 
	SOI 
	~ ( (NEWLINE | whitespace)* ~ prod ~ (NEWLINE | whitespace)* )*
	~ EOI
}